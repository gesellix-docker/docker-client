import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath "gradle.plugin.net.ossindex:ossindex-gradle-plugin:0.1.1"
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.11'
}

subprojects {
    ext {
        artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
        bintrayDryRun = false
    }

    apply plugin: 'groovy'
    apply plugin: "net.ossindex.audit"
    apply plugin: 'com.github.ben-manes.versions'

    group = 'de.gesellix'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    def dependencyVersions = [
            'com.squareup.okio:okio:1.14.0',
            'org.bouncycastle:bcpkix-jdk15on:1.59',
            'org.bouncycastle:bcprov-jdk15on:1.59',
            'org.codehaus.groovy:groovy:2.4.15',
            'org.codehaus.groovy:groovy-json:2.4.15',
            'org.jetbrains.kotlin:kotlin-reflect:1.2.21',
            'org.jetbrains.kotlin:kotlin-stdlib:1.2.21',
    ]

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
        }
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
    distributionType = Wrapper.DistributionType.ALL
}

apply from: 'debug.gradle'
