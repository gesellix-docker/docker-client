import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath "gradle.plugin.net.ossindex:ossindex-gradle-plugin:0.3.21"
    }
}

plugins {
}

subprojects {
    ext {
        artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
        bintrayDryRun = false
    }

    apply plugin: 'groovy'
    apply plugin: "net.ossindex.audit"
    apply plugin: 'com.github.ben-manes.versions'

    group = 'de.gesellix'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    def dependencyVersions = [
            'com.squareup.okio:okio:2.1.0',
            'org.codehaus.groovy:groovy:2.5.4',
            'org.codehaus.groovy:groovy-json:2.5.4',
            'org.jetbrains.kotlin:kotlin-reflect:1.3.10',
            'org.jetbrains.kotlin:kotlin-stdlib:1.3.10',
    ]

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
        }
    }
}

task updateWrapper(type: Wrapper) {
    gradleVersion = '5.0'
    distributionType = Wrapper.DistributionType.ALL
}

apply from: 'debug.gradle'
