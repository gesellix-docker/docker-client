import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath "gradle.plugin.net.ossindex:ossindex-gradle-plugin:0.1.1"
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.11'
}

subprojects {
    ext {
        artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
        bintrayDryRun = false
    }

    apply plugin: 'groovy'
    apply plugin: "net.ossindex.audit"
    apply plugin: 'com.github.ben-manes.versions'

    group = 'de.gesellix'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    def dependencyVersions = [
            'com.squareup.okhttp3:okhttp:3.11.0',
            'com.squareup.okio:okio:1.16.0',
            'org.apache.commons:commons-compress:1.18',
            'org.bouncycastle:bcpkix-jdk15on:1.60',
            'org.bouncycastle:bcprov-jdk15on:1.60',
            'org.codehaus.groovy:groovy:2.5.3',
            'org.codehaus.groovy:groovy-json:2.5.3',
            'org.jetbrains.kotlin:kotlin-reflect:1.2.21',
            'org.jetbrains.kotlin:kotlin-stdlib:1.2.21',
    ]

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
        }
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

task updateWrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
    distributionType = Wrapper.DistributionType.ALL
}

apply from: 'debug.gradle'
